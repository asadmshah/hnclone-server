apply plugin: 'com.google.protobuf'

version '0.1.0'

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile group: 'com.google.protobuf', name: 'protobuf-java', version: "$protoc_version"

    compile group: 'io.grpc', name: 'grpc-protobuf-lite', version: "$grpc_version"
    compile group: 'io.grpc', name: 'grpc-stub', version: "$grpc_version"
}

protobuf {
    generatedFilesBaseDir = "$projectDir/gen"

    protoc {
        artifact = "com.google.protobuf:protoc:$protoc_version"
    }

    plugins {
        javalite {
            artifact = "com.google.protobuf:protoc-gen-javalite:$protoc_version"
        }
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:$grpc_version"
        }
    }

    generateProtoTasks {
        ofSourceSet('main')*.plugins {
            // Apply the "grpc" plugin whose spec is defined above, without
            // options.  Note the braces cannot be omitted, otherwise the
            // plugin will not be added. This is because of the implicit way
            // NamedDomainObjectContainer binds the methods.
            grpc {
                option 'lite'
            }
        }
    }
}

clean {
    delete protobuf.generatedFilesBaseDir
}

idea {
    module {
        sourceDirs += file("${protobuf.generatedFilesBaseDir}/main/java");
        sourceDirs += file("${protobuf.generatedFilesBaseDir}/main/grpc");
    }
}